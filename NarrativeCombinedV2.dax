NarrativeCombinedV2 = 
VAR CurrentYear = 
    CALCULATE(
        MAX(data_obesity[periodLabel]),
        FILTER(
            data_obesity,
            NOT(ISBLANK(data_obesity[value]))
        )
    )

VAR AreaLabel = SELECTEDVALUE(data_obesity[areaLabel])
VAR MetricTypeAltLabel = SELECTEDVALUE(data_obesity[metricTypeAltLabel])

VAR CurrentValue = 
    CALCULATE(
        SUM(data_obesity[value]),
        data_obesity[periodLabel] = CurrentYear
    )

-- Find the most recent previous year with data, skipping blank values
VAR thePreviousYear = 
    MAXX(
        FILTER(
            ALL(data_obesity), 
            data_obesity[periodLabel] < CurrentYear && 
            NOT(ISBLANK(data_obesity[value]))
        ), 
        data_obesity[periodLabel]
    )
    
-- Calculate PreviousYearValue, but only if the previous year has valid data
VAR PreviousYearValue = 
    IF(
        NOT(ISBLANK(thePreviousYear)),
        CALCULATE(
            SUM(data_obesity[value]),
            data_obesity[periodLabel] = thePreviousYear,
            NOT(ISBLANK(data_obesity[value]))
        ),
        BLANK()
    )

VAR FiveYearsAgo = CurrentYear - 5
VAR earliestYear = CALCULATE(MIN(data_obesity[periodLabel]))

VAR validYear = IF(FiveYearsAgo < earliestYear, earliestYear, FiveYearsAgo)
VAR validYearValue = 
    CALCULATE(
        SUM(data_obesity[value]),
        data_obesity[periodLabel] = validYear
    )

VAR yearDifference = CurrentYear - validYear
VAR previousYearsCalc = CurrentValue - validYearValue

VAR FiveYearChange =
    IF(
        NOT(ISBLANK(validYearValue)),
        ((CurrentValue - validYearValue) / ABS(validYearValue)) * 100,
        BLANK()
    )

VAR RankMe = 
    VAR SelectedMetric = SELECTEDVALUE(data_obesity[metricTypeLabel])
    VAR SelectedArea = SELECTEDVALUE(data_obesity[areaLabel])
    RETURN
    LOOKUPVALUE(
        rankingData[value],
        rankingData[metricTypeLabel], SelectedMetric,
        rankingData[areaLabel], SelectedArea
    )

VAR TotalPlaces =
    VAR SelectedMetric = SELECTEDVALUE(data_obesity[metricTypeAltLabel])
    RETURN
    CALCULATE(
        COUNTROWS(rankingData),
        rankingData[metricTypeAltLabel] = SelectedMetric
    )

VAR incDec = 
    IF(previousYearsCalc >= 0,
    "increase",
    "decrease"
    )

-- Conditional narrative based on availability of previous year data
VAR PreviousYearSection = 
    IF(
        NOT(ISBLANK(PreviousYearValue)),
        "The previous year (" & thePreviousYear & ") had a recorded value of " &
        FORMAT(PreviousYearValue, "0.00") & "%, so this represents a " &
        FORMAT(CurrentValue - PreviousYearValue, "0.00") & " percentage point difference.",
        ""
    )

RETURN
    "In " & CurrentYear & " in " & AreaLabel & ", the " & MetricTypeAltLabel & " was recorded at " &
    FORMAT(CurrentValue, "0.00") & "%. " & PreviousYearSection & 
    " Over the past " & yearDifference & " years, there has been an " &
    FORMAT(ABS(previousYearsCalc), "0.00") & " percentage point " & incDec & ". " & AreaLabel & " ranks " & 
    RankMe & " out of " & TotalPlaces &
    " local authorities for this indicator where 1 is best."
